
chunk       ::= stmt* EOF

stmt        ::= assign ';'
              | 'if' '(' expr ')' stmt ( 'else' stmt )?
              | 'while' '(' expr ')' stmt
              | 'do' stmt 'while' '(' expr ')' ';'
              | 'for' '(' assign? ';' expr? ';' assign? ')' stmt
              | 'continue' ';'
              | 'break' ';'
              | 'echo' expr ';'
              | '{' stmt* '}'

assign      ::= VARNAME ( '=' | '+=' | '-=' | '*=' | '/=' | '%=' ) expr
              | VARNAME ( '++' | '--' )

expr        ::= and_expr ( '||' and_expr )*

and_expr    ::= equal_expr ( '&&' equal_expr )*

equal_expr  ::= comp_expr ( ( '==' | '!=' ) comp_expr )*

comp_expr   ::= add_expr ( ( '>' | '>=' | '<' | '<=' ) add_expr )*

add_expr    ::= mul_expr ( ( '+' | '-' ) mul_expr )*

mul_expr    ::= unary_expr ( ( '*' | '/' | '%' ) unary_expr )*

unary_expr  ::= ( '+' | '-' | '!' ) unary_expr | prim_expr

prim_expr   ::= 'null' | 'false' | 'true' | INT | FLOAT | STRING
              | '[' ( expr ( ',' expr )* )? ']'
              | VARNAME
              | '(' expr ')'

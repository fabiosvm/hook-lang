
chunk       ::= stmt* EOF

stmt        ::= block
              | var_decl ';'
              | assign_call ';'
              | 'struct' NAME '{' ( NAME ( ',' NAME )* )? '}'
              | 'fn' NAME '(' ( 'mut'? NAME ( ',' 'mut'? NAME )* )? ')' block
              | 'delete' NAME subscr* '[' expr ']' ';'
              | 'if' '(' expr ')' stmt ( 'else' stmt )?
              | 'loop' block
              | 'while' '(' expr ')' stmt
              | 'do' stmt 'while' '(' expr ')' ';'
              | 'for' '(' var_decl | assign_call? ';' expr? ';' assign_call? ')' stmt
              | 'continue' ';'
              | 'break' ';'
              | 'return' expr? ';'

block       ::= '{' stmt* '}'

var_decl    ::= 'let' NAME '=' expr
              | 'let' 'mut' NAME ( '=' expr )?
              | 'let' '[' 'mut'? NAME ( ',' 'mut'? NAME )* ']' '=' expr
              | 'let' '{' 'mut'? NAME ( ',' 'mut'? NAME )* '}' '=' expr

assign_call ::= NAME subscr* ( '=' | '+=' | '-=' | '*=' | '/=' | '%=' ) expr
              | NAME subscr* ( '++' | '--' )
              | NAME subscr* '[' ']' '=' expr
              | NAME subscr* subscr '=' expr
              | NAME ( subscr | call )* call

subscr      ::= '[' expr ']' | '.' NAME 

call        ::= '(' ( expr ( ',' expr )* )? ')'

expr        ::= and_expr ( '||' and_expr )*

and_expr    ::= equal_expr ( '&&' equal_expr )*

equal_expr  ::= comp_expr ( ( '==' | '!=' ) comp_expr )*

comp_expr   ::= add_expr ( ( '>' | '>=' | '<' | '<=' ) add_expr )*

add_expr    ::= mul_expr ( ( '+' | '-' ) mul_expr )*

mul_expr    ::= unary_expr ( ( '*' | '/' | '%' ) unary_expr )*

unary_expr  ::= ( '-' | '!' ) unary_expr | prim_expr

prim_expr   ::= 'null' | 'false' | 'true' | INT | FLOAT | STRING
              | '[' ( expr ( ',' expr )* )? ']'
              | '{' ( NAME ':' expr ( ',' NAME ':' expr )* )? '}'
              | 'struct' '{' ( NAME ( ',' NAME )* )? '}'
              | 'fn' '(' ( 'mut'? NAME ( ',' 'mut'? NAME )* )? ')' block
              | 'if' '(' expr ')' expr 'else' expr
              | NAME ( subscr | call )* ( '{' ( expr ( ',' expr )* )? '}' )?
              | '(' expr ')'
